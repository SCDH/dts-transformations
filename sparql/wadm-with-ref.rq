# SPARQL for constructing a WADM selector for the output of the
# document endpoint queried with a ref parameter. The input graph must
# be the output of a navigation endpoint for the same citation tree of
# the same resource.
#
# Parameters to be set:
#
# ?PARAMTREE - the label of the citation tree, empty string for default
# ?PARAMREF - the identifier member passed to the document enpoint as ref parameter
# ?PARAMURI - the document query URL 

PREFIX dts: <https://w3id.org/dts/api#>
PREFIX oa:  <http://www.w3.org/ns/oa#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>


CONSTRUCT {
  ?PARAMURI rdf:type oa:SpecificResource .
  ?PARAMURI oa:hasSource ?resource .
  ?PARAMURI oa:hasSelector _:xps .
  _:xps rdf:type oa:XPathSelector .
  _:xps rdf:value ?xpath .
  ?PARAMURI oa:hasSelector _:fgs .
  _:fgs rdf:type oa:FragmentSelector .
  _:fgs dcterms:conformsTo dts: .
  _:fgs rdf:value ?DTSSEL .

  _:fgs dts:isMember _:m .
  _:m dts:identifier ?PARAMREF .
  _:m dts:citeType ?citeType .
  _:m rdf:type dts:CiteableUnit .
  _:m dts:fromTree ?PARAMTREE .
  _:m dts:level ?level .
  _:m dts:parent ?parent .

  ?PARAMURI dts:citeType ?citeType .
}

WHERE {

  # ?PARAM* must be passed in as parameters
  BIND("wadm" as ?PARAMTREE) . # empty value means the default tree?
  BIND("John:1:3" as ?PARAMREF) .
  BIND(<https://example.com/api/dts/document?resource=https://coexist.org/b/john.xml&ref=John:1:3> as ?PARAMURI) .


  BIND(CONCAT("tree=", STR($PARAMTREE), "&ref=", STR(?PARAMREF)) as ?DTSSEL) .

  ?resource rdf:type dts:Resource .
  ?member rdf:type dts:CitableUnit .
  ?member dts:identifier ?PARAMREF .
  ?member dts:xpath ?xpath .
  ?member dts:citeType ?citeType .
  ?member dts:level ?level .
  ?member dts:parent ?parent .

}
